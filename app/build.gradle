apply plugin: "com.android.application"

android {
    namespace "com.eurokonverter"
    compileSdk 35

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    defaultConfig {
        applicationId "com.eurokonverter"
        minSdk 15   // android 4.0.4
        targetSdk 35
        versionCode 11
        versionName "3.0"

        setProperty("archivesBaseName", "$applicationId-$versionName")
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
        }
    }

    androidResources {
        generateLocaleConfig true
    }

    flavorDimensions = ["distribution"]
    productFlavors {
        gplay {
            minSdk 21   // because of the admob library
            dimension "distribution"
        }
        fdroid {
            dimension "distribution"

            dependenciesInfo {
                includeInApk = false
                includeInBundle = false
            }
        }
    }
}

dependencies {

    //  version 1.7.0+ requires minSdkVersion 21
    implementation "androidx.appcompat:appcompat:1.6.1"

    // version 1.12.0+ requires minSdkVersion 19
    implementation "com.google.android.material:material:1.11.0"

    //  version 1.1.0+ requires minSdkVersion 21
    implementation "androidx.gridlayout:gridlayout:1.0.0"

    implementation "androidx.cardview:cardview:1.0.0"

    // version 24.0.0+ requires min SDK 23
    gplayImplementation "com.google.android.gms:play-services-ads:23.6.0"
}